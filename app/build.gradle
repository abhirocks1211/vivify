apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    def isRunningOnTravis = System.getenv("CI") == "true"
    defaultConfig {
        applicationId "com.rva.mrb.vivify"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }
        def spotifyClientId
        if (isRunningOnTravis) {
            spotifyClientId = System.getenv("spotify_client_id")
        } else {
            spotifyClientId = SPOTIFY_CLIENT_ID
        }
        buildConfigField("String", "SPOTIFY_CLIENT_ID", spotifyClientId)
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['jni'] // copy folder that includes *.so to package
        }
    }
    signingConfigs {
        if (isRunningOnTravis) {
            travis {
                storeFile file("../android.jks")
                storePassword System.getenv("release_store_pass")
                keyAlias System.getenv("release_key_pass")
                keyPassword System.getenv("release_key_alias")
            }
        } else {
            local {
                storeFile file(PATH_TO_CERT_FILE)
                storePassword RELEASE_STORE_PASS
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASS
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isRunningOnTravis) {
                signingConfig signingConfigs.travis
            } else {
                signingConfig signingConfigs.local
            }
        }
        debug {
            debuggable true
        }
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    // todo remove stetho
    maven { url 'https://github.com/WickeDev/stetho-realm/raw/master/maven-repo' }
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.1.0'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.0.2'
    compileOnly 'javax.annotation:jsr250-api:1.0'
    testImplementation 'junit:junit:4.12'
    implementation 'com.spotify.sdk:spotify-android-auth-1.0.0@aar'
    implementation 'com.spotify.sdk:spotify-player-24-noconnect-2.20b@aar'
    implementation 'com.bignerdranch.android:expandablerecyclerview:2.1.1'
    implementation 'com.android.support:recyclerview-v7:25.2.0'
    implementation 'com.android.support:design:25.2.0'
    implementation 'io.realm:android-adapters:2.1.1'
    implementation 'com.android.support:cardview-v7:25.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.jakewharton:butterknife:8.1.0'
    implementation 'com.google.dagger:dagger:2.0.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.0.5'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.uphyca:stetho_realm:2.2.2'
    implementation 'com.android.support:appcompat-v7:25.2.0'
    implementation 'com.android.support:support-v4:25.2.0'
    implementation 'com.github.bumptech.glide:glide:3.6.1'
    implementation 'org.parceler:parceler-api:1.1.5'
    annotationProcessor 'org.parceler:parceler:1.1.5'
    implementation 'com.android.support:palette-v7:25.2.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    implementation 'com.google.firebase:firebase-core:11.4.2'
}
apply plugin: 'com.google.gms.google-services'